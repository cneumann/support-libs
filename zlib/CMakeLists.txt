
CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

IF(ZLIB_SRC_DIR)

    PROJECT(zlib C)

    # sanity check ZLIB_SRC_DIR
    IF(NOT EXISTS "${ZLIB_SRC_DIR}/zlib.h")
        MESSAGE(SEND_ERROR "No file 'zlib.h' in ZLIB_SRC_DIR = '${ZLIB_SRC_DIR}'")
        RETURN()
    ENDIF(NOT EXISTS "${ZLIB_SRC_DIR}/zlib.h")

    SET(TARGET_NAME_SHARED ${ZLIB_TARGET_NAME_SHARED})
    SET(TARGET_NAME_STATIC ${ZLIB_TARGET_NAME_STATIC})

    IF(SEPARATE_INSTALL_DIRS)
        SET(ZLIB_RUNTIME_INSTALL_DIR "zlib/bin")
        SET(ZLIB_LIBRARY_INSTALL_DIR "zlib/lib")
        SET(ZLIB_ARCHIVE_INSTALL_DIR "zlib/lib")
        SET(ZLIB_HEADER_INSTALL_DIR  "zlib/include")
    ELSE(SEPARATE_INSTALL_DIRS)
        SET(ZLIB_RUNTIME_INSTALL_DIR "bin")
        SET(ZLIB_LIBRARY_INSTALL_DIR "lib")
        SET(ZLIB_ARCHIVE_INSTALL_DIR "lib")
        SET(ZLIB_HEADER_INSTALL_DIR  "include")
    ENDIF(SEPARATE_INSTALL_DIRS)

    SET(C_SRC_FILES)
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/adler32.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/compress.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/crc32.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/deflate.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/gzclose.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/gzlib.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/gzread.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/gzwrite.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/infback.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/inffast.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/inflate.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/inftrees.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/trees.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/uncompr.c")
    LIST(APPEND C_SRC_FILES "${ZLIB_SRC_DIR}/zutil.c")

    SET(C_HDR_FILES)
    LIST(APPEND C_HDR_FILES "${ZLIB_SRC_DIR}/zconf.h")
    LIST(APPEND C_HDR_FILES "${ZLIB_SRC_DIR}/zlib.h")

    SET(TEST0_C_SRC_FILES)
    LIST(APPEND TEST0_C_SRC_FILES "${ZLIB_SRC_DIR}/test/example.c")

    SET(TEST0_TARGET_NAME_SHARED "zlib-example-shared")
    SET(TEST0_TARGET_NAME_STATIC "zlib-example-static")

    INCLUDE_DIRECTORIES("${ZLIB_SRC_DIR}")

    IF(BUILD_SHARED_LIBS)
        ADD_LIBRARY(${TARGET_NAME_SHARED} SHARED ${C_SRC_FILES})

        SET_TARGET_PROPERTIES(${TARGET_NAME_SHARED}  PROPERTIES
            DEFINE_SYMBOL ZLIB_DLL
            DEBUG_POSTFIX "d")

        INSTALL(TARGETS ${TARGET_NAME_SHARED}
            CONFIGURATIONS Release Debug
            RUNTIME DESTINATION ${ZLIB_RUNTIME_INSTALL_DIR}
            LIBRARY DESTINATION ${ZLIB_LIBRARY_INSTALL_DIR}
            ARCHIVE DESTINATION ${ZLIB_ARCHIVE_INSTALL_DIR})

        IF(BUILD_TESTS)
            ADD_EXECUTABLE(${TEST0_TARGET_NAME_SHARED} ${TEST0_C_SRC_FILES})
            TARGET_LINK_LIBRARIES(${TEST0_TARGET_NAME_SHARED} ${TARGET_NAME_SHARED})
        ENDIF(BUILD_TESTS)
    ENDIF(BUILD_SHARED_LIBS)

    IF(BUILD_STATIC_LIBS)
        ADD_LIBRARY(${TARGET_NAME_STATIC} STATIC ${C_SRC_FILES})

        SET_TARGET_PROPERTIES(${TARGET_NAME_STATIC}  PROPERTIES
            DEBUG_POSTFIX "d")

        INSTALL(TARGETS ${TARGET_NAME_STATIC}
            CONFIGURATIONS Release Debug
            RUNTIME DESTINATION ${ZLIB_RUNTIME_INSTALL_DIR}
            LIBRARY DESTINATION ${ZLIB_LIBRARY_INSTALL_DIR}
            ARCHIVE DESTINATION ${ZLIB_ARCHIVE_INSTALL_DIR})

        IF(BUILD_TESTS)
            ADD_EXECUTABLE(${TEST0_TARGET_NAME_STATIC} ${TEST0_C_SRC_FILES})
            TARGET_LINK_LIBRARIES(${TEST0_TARGET_NAME_STATIC} ${TARGET_NAME_STATIC})
        ENDIF(BUILD_TESTS)
    ENDIF(BUILD_STATIC_LIBS)

    INSTALL(FILES ${C_HDR_FILES}
        DESTINATION ${ZLIB_HEADER_INSTALL_DIR}
        PERMISSIONS OWNER_READ OWNER_WRITE
                    GROUP_READ
                    WORLD_READ)

ELSE(ZLIB_SRC_DIR)
    MESSAGE(STATUS "ZLIB_SRC_DIR not set - can not build zlib")
ENDIF(ZLIB_SRC_DIR)
